# Note that if you are working on the analysis development locally, i.e. outside
# of the REANA platform, you can proceed as follows:
#
#   $ cd reana-demo-cms-h4l
#   $ mkdir snakemake-local-run
#   $ cd snakemake-local-run
#   $ virtualenv ~/.virtualenvs/cms-h4l-snakemake
#   $ source ~/.virtualenvs/cms-h4l-snakemake/bin/activate
#   $ pip install snakemake
#   $ cp -a ../code ../data .
#   $ snakemake -s ../workflow/snakemake/Snakefile -p --cores 4 --use-singularity
#           --configfile ../workflow/snakemake/input.yaml


rule all:
    input:
        "results/mass4l_combine_userlvl3.pdf"

rule scram:
    input:
        config["data"],
        config["code"]
    output:
        touch("results/scramdone.txt")
    container:
        "docker://cmsopendata/cmssw_5_3_32"
    shell:
        "source /opt/cms/cmsset_default.sh "
        "&& scramv1 project CMSSW CMSSW_5_3_32 "
        "&& cd CMSSW_5_3_32/src "
        "&& eval `scramv1 runtime -sh` "
        "&& cp -r ../../code/HiggsExample20112012 . "
        "&& cd HiggsExample20112012/HiggsDemoAnalyzer "
        "&& scram b "
        "&& cd ../Level3 "
        "&& mkdir -p ../../../../results "

rule analyze_data:
    input:
        config["data"],
        config["code"],
        "results/scramdone.txt"
    output:
        "results/DoubleMuParked2012C_10000_Higgs.root"
    container:
        "docker://cmsopendata/cmssw_5_3_32"
    shell:
        "source /opt/cms/cmsset_default.sh "
        "&& cd CMSSW_5_3_32/src "
        "&& eval `scramv1 runtime -sh` "
        "&& cd HiggsExample20112012/HiggsDemoAnalyzer "
        "&& cd ../Level3 "
        "&& cmsRun demoanalyzer_cfg_level3data.py"

rule analyze_mc:
    input:
        config["data"],
        config["code"],
        "results/scramdone.txt"
    output:
        "results/Higgs4L1file.root"
    container:
        "docker://cmsopendata/cmssw_5_3_32"
    shell:
        "source /opt/cms/cmsset_default.sh "
        "&& cd CMSSW_5_3_32/src "
        "&& eval `scramv1 runtime -sh` "
        "&& cd HiggsExample20112012/HiggsDemoAnalyzer "
        "&& cd ../Level3 "
        "&& cmsRun demoanalyzer_cfg_level3MC.py"

rule make_plot:
    input:
        config["data"],
        config["code"],
        "results/DoubleMuParked2012C_10000_Higgs.root",
        "results/Higgs4L1file.root"
    output:
        "results/mass4l_combine_userlvl3.pdf"
    container:
        "docker://cmsopendata/cmssw_5_3_32"
    shell:
        "source /opt/cms/cmsset_default.sh "
        "&& cd CMSSW_5_3_32/src "
        "&& eval `scramv1 runtime -sh` "
        "&& cd HiggsExample20112012/HiggsDemoAnalyzer "
        "&& cd ../Level3 "
        "&& root -b -l -q ./M4Lnormdatall_lvl3.cc"
